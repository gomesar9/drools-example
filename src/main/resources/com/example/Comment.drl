package com.example
 
import com.example.proto.CommentProto.Comment;

global java.util.List list
 
rule "Check sender and receiver"
    when
        $comment: Comment( sender == null || sender.isEmpty() || receiver == null || receiver.isEmpty() )
    then
        Comment.Builder commentBuilder = $comment.toBuilder();
        if ( $comment.getSender() == null || $comment.getSender().isEmpty()) {
            commentBuilder.setSender("anonymous");
        }
        if ( $comment.getReceiver() == null || $comment.getReceiver().isEmpty()) {
            commentBuilder.setReceiver("anonymous");
        }
        delete($comment);
        Comment updatedComment = commentBuilder.build();
        insert(updatedComment);
        System.out.println("anonymous sender or receiver in comment: " + $comment.getText());
end

rule "Set priority to Urgent if message contains 'urgente'"
    when
        $comment: Comment( text != null && text.toLowerCase().contains("urgente") && priority != Comment.Priority.URGENT)
    then
        Comment updatedComment = $comment.toBuilder().setPriority(Comment.Priority.URGENT).build();
        delete($comment);
        insert(updatedComment);
        System.out.println("Priority set to URGENT for comment: " + $comment.getText());
end


rule "Set priority to Normal if message do not contains 'urgente'"
    when
        $comment: Comment( !text.toLowerCase().contains("urgente") && priority == Comment.Priority.URGENT)
    then
        Comment updatedComment = $comment.toBuilder().setPriority(Comment.Priority.NORMAL).build();
        delete($comment);
        insert(updatedComment);
        System.out.println("Priority set to Normal for comment: " + $comment.getText());
end
